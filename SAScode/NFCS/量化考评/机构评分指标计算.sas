/*%MACRO SCORE(STAT_DT);*/
%LET LAST_DT=INTNX('MONTH',&STAT_DT,-3,'END');

/*客户数指标计算*/
DATA SINO_PERSON1;
	SET SSS.SINO_PERSON(WHERE=(SUBSTR(SORGCODE,1,1)='Q' AND SORGCODE^='Q10152900H0000' AND DATEPART(DGETDATE)<=&STAT_DT));
	NAME=PUT(SORGCODE,$SHORT_CD.);
RUN;

PROC SQL;
	CREATE TABLE SINO_PERSON2 AS SELECT
		NAME
		,COUNT(*) AS PERSONS
	FROM SINO_PERSON1
	GROUP BY NAME
	;
QUIT;

DATA SINO_PERSON3;
	SET SSS.SINO_PERSON(WHERE=(SUBSTR(SORGCODE,1,1)='Q' AND SORGCODE^='Q10152900H0000' AND DATEPART(DGETDATE)<=&LAST_DT));
	NAME=PUT(SORGCODE,$SHORT_CD.);
RUN;

PROC SQL;
	CREATE TABLE SINO_PERSON4 AS SELECT
		NAME
		,COUNT(*) AS LASTPERSONS
	FROM SINO_PERSON3
	GROUP BY NAME
	;
QUIT;

PROC SQL;
	CREATE TABLE SINO_PERSON AS SELECT
		T1.NAME LABEL="机构名称"
		,T1.PERSONS LABEL="全量客户数"
		,COALESCE(T1.PERSONS-T2.LASTPERSONS,0) AS NEWPERSONS "季度新增客户数"
	FROM SINO_PERSON2 AS T1
	LEFT JOIN SINO_PERSON4 AS T2
	ON T1.NAME=T2.NAME
	;
QUIT;

/*业务数指标计算*/

PROC SORT DATA=SSS.SINO_LOAN(WHERE=(SUBSTR(SORGCODE,1,1)='Q' AND SORGCODE^='Q10152900H0000' AND DATEPART(DGETDATE)<=&STAT_DT))  OUT=SINO_LOAN1 ;
	BY SORGCODE SACCOUNT DGETDATE;
RUN;
DATA SINO_LOAN2;
	SET SINO_LOAN1;
	BY SORGCODE SACCOUNT DGETDATE;
	IF LAST.SACCOUNT;
	NAME=PUT(SORGCODE,$SHORT_CD.);
RUN;
PROC SQL;
	CREATE TABLE SINO_LOAN3 AS SELECT
		NAME
		,COUNT(*) AS LOAN_CNT
		,SUM(ICREDITLIMIT) AS LOAN_AMT
	FROM SINO_LOAN2
	GROUP BY NAME
	;
QUIT;

PROC SORT DATA=SSS.SINO_LOAN(WHERE=(SUBSTR(SORGCODE,1,1)='Q' AND SORGCODE^='Q10152900H0000' AND DATEPART(DGETDATE)<=&LAST_DT))  OUT=SINO_LOAN4 ;
	BY SORGCODE SACCOUNT DGETDATE;
RUN;
DATA SINO_LOAN5;
	SET SINO_LOAN4;
	BY SORGCODE SACCOUNT DGETDATE;
	IF LAST.SACCOUNT;
	NAME=PUT(SORGCODE,$SHORT_CD.);
RUN;
PROC SQL;
	CREATE TABLE SINO_LOAN6 AS SELECT
		NAME
		,COUNT(*) AS LOAN_CNT
		,SUM(ICREDITLIMIT) AS LOAN_AMT
	FROM SINO_LOAN5
	GROUP BY NAME
	;
QUIT;

PROC SQL;
	CREATE TABLE SINO_LOAN AS SELECT
		T1.NAME LABEL="机构名称"
		,T1.LOAN_CNT LABEL="贷款业务数"
		,COALESCE(T1.LOAN_CNT-T2.LOAN_CNT,0) AS NEWLOAN "季度新增贷款业务数"
		,T1.LOAN_AMT/10000 LABEL="贷款金额(万元)"
		,COALESCE(T1.LOAN_AMT-T2.LOAN_AMT,0)/10000 AS NEWAMT "季度新增贷款金额(万元)"
	FROM SINO_LOAN3 AS T1
	LEFT JOIN SINO_LOAN6 AS T2
	ON T1.NAME=T2.NAME
	;
QUIT;

/*查询量指标计算*/

DATA SINO_RECORD1;
	SET SSS.Sino_credit_record(WHERE=(SUBSTR(SORGCODE,1,1)='Q' AND SORGCODE^='Q10152900H0000' AND &LAST_DT<=DATEPART(DREQUESTTIME)<=&STAT_DT));
	NAME=PUT(SORGCODE,$SHORT_CD.);
	IF SSERIALNUMBER='null' THEN SFCD='未查得'; ELSE SFCD='查得';
RUN;

PROC SQL;
	CREATE TABLE SINO_RECORD AS SELECT
		NAME LABEL="机构名称"
		,COUNT(*) AS CDS LABEL="查询量"
	FROM SINO_RECORD1
	GROUP BY NAME;
QUIT;


LIBNAME MYXLS EXCEL "C:\Users\Data Analyst\Desktop\常用代码\自动化\结果文件夹\机构评分结果\评分结果.XLS";

DATA MYXLS."客户数指标计算"n(dblabel=YES);
	SET SINO_PERSON;
RUN;

DATA MYXLS."业务数指标计算"n(dblabel=YES);
	SET SINO_LOAN;
RUN;

DATA MYXLS."查询指标计算"n(dblabel=YES);
	SET SINO_RECORD;
RUN;

LIBNAME MYXLS CLEAR;

PROC SORT DATA=SSS.SINO_LOAN(WHERE=(SORGCODE NOT IN ('11111111111111','Q10152900H0000')))   OUT=ORG_FIRST1;
	BY SORGCODE DDATEOPENED;
RUN;

DATA ORG_FIRST2;
	SET ORG_FIRST1;
	BY SORGCODE DDATEOPENED;
	IF FIRST.SORGCODE;
RUN;

DATA ORG_FIRST;
	LENGTH ORGNAME $20.;
	FORMAT FIRST_OPEN YYMMDD10.;
	INFORMAT FIRST_OPEN YYMMDD10.;
	SET ORG_FIRST2(KEEP= SORGCODE DDATEOPENED);
	ORGNAME=PUT(SORGCODE,$SHORT_CD.);
	FIRST_OPEN=DATEPART(DDATEOPENED);
	DROP SORGCODE DDATEOPENED;
RUN;


PROC EXPORT DATA=ORG_FIRST OUTFILE = "C:\Users\Data Analyst\Desktop\常用代码\自动化\结果文件夹\机构评分结果\评分结果.XLS"
	DBMS=EXCEL REPLACE;
RUN;

/*%MEND SCORE;*/



